{
  parserClass="com.jeremyworboys.expressionengine.parser.ExpressionEngineParser"
  psiPackage="com.jeremyworboys.expressionengine.psi"
  psiImplPackage="com.jeremyworboys.expressionengine.psi.impl"
  psiClassPrefix="ExpressionEngine"
  psiImplClassSuffix="Impl"

  elementTypeHolderClass="com.jeremyworboys.expressionengine.psi.ExpressionEngineTypes"
  elementTypeClass="com.jeremyworboys.expressionengine.psi.ExpressionEngineElementType"
  tokenTypeClass="com.jeremyworboys.expressionengine.psi.ExpressionEngineTokenType"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"
  extends(".*expr")=expr
}

expressionEngineFile ::= statement*
private statement ::= module | conditional | special_tag | variable_tag | comment | T_CONTENT+

// Literals
path_literal ::= T_PATH_LITERAL
number_literal ::= T_NUMBER_LITERAL
string_literal ::= T_STRING_START T_STRING_CONTENT* T_STRING_END
boolean_literal ::= T_TRUE | T_FALSE

// Comments
comment ::= T_COMMENT

// Tag params
private tag_param ::= T_PARAM_NAME T_EQUAL tag_param_value
private tag_param_value ::= path_literal | number_literal | string_literal | boolean_literal

// Special tags
private special_tag ::= path_tag | embed_tag | layout_tag | redirect_tag
path_tag ::= T_LD T_PATH T_EQUAL tag_param_value T_RD
embed_tag ::= T_LD T_EMBED T_EQUAL tag_param_value tag_param* T_RD
layout_tag ::= T_LD T_LAYOUT T_EQUAL tag_param_value tag_param* T_RD
redirect_tag ::= T_LD T_REDIRECT T_EQUAL tag_param_value T_RD

// Variables
private variable_tag ::= variable | variable_pair
variable ::= T_LD T_VARIABLE_NAME tag_param* T_RD
variable_pair ::= variable_open_tag statement* variable_close_tag
variable_open_tag ::= T_LD T_VARIABLE_NAME tag_param* T_RD
variable_close_tag ::= T_LD T_SLASH T_VARIABLE_NAME T_RD

// Modules
module ::= module_open_tag statement* module_close_tag
module_open_tag  ::= T_LD T_MODULE_NAME tag_param* T_RD
module_close_tag ::= T_LD T_SLASH T_MODULE_NAME T_RD

// Conditionals
conditional ::= conditional_if statement* (conditional_elseif statement*)* (conditional_else statement*)? conditional_endif
conditional_if     ::= T_LD T_IF expr T_RD {pin=2}
conditional_elseif ::= T_LD T_ELSEIF expr T_RD {pin=2}
conditional_else   ::= T_LD T_ELSE T_RD
conditional_endif  ::= T_LD T_SLASH T_IF T_RD

// Conditional expressions
expr ::= group_bool | group_cmp | group_add | group_mul | group_unary | op_pow_expr | op_concat_expr | group_primary

private group_bool    ::= op_and_expr | op_xor_expr | op_or_expr
private group_cmp     ::= op_eq_expr | op_neq_expr | op_lt_expr | op_lte_expr | op_gt_expr | op_gte_expr | op_starts_expr | op_contains_expr | op_ends_expr | op_match_expr
private group_mul     ::= op_mul_expr | op_div_expr | op_mod_expr
private group_add     ::= op_add_expr | op_sub_expr
private group_unary   ::= unary_not_expr | unary_plus_expr | unary_minus_expr
private group_primary ::= literal_expr | paren_expr

op_eq_expr       ::= expr T_OP_EQ expr
op_neq_expr      ::= expr T_OP_NEQ expr
op_lt_expr       ::= expr T_OP_LT expr
op_lte_expr      ::= expr T_OP_LTE expr
op_gt_expr       ::= expr T_OP_GT expr
op_gte_expr      ::= expr T_OP_GTE expr
op_starts_expr   ::= expr T_OP_STARTS expr
op_contains_expr ::= expr T_OP_CONTAINS expr
op_ends_expr     ::= expr T_OP_ENDS expr
op_match_expr    ::= expr T_OP_MATCH expr
op_and_expr      ::= expr T_OP_AND expr
op_xor_expr      ::= expr T_OP_XOR expr
op_or_expr       ::= expr T_OP_OR expr
op_add_expr      ::= expr T_OP_ADD expr
op_sub_expr      ::= expr T_OP_SUB expr
op_mul_expr      ::= expr T_OP_MUL expr
op_div_expr      ::= expr T_OP_DIV expr
op_pow_expr      ::= expr T_OP_POW expr
op_mod_expr      ::= expr T_OP_MOD expr
op_concat_expr   ::= expr T_OP_CONCAT expr
unary_not_expr   ::= T_OP_NOT expr
unary_plus_expr  ::= T_OP_ADD expr
unary_minus_expr ::= T_OP_SUB expr
paren_expr       ::= T_LP expr T_RP
literal_expr     ::= number_literal | string_literal | boolean_literal | T_VARIABLE_NAME

